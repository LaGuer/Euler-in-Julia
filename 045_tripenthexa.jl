
"""
tri_penta_hexa_num([k_min=144])
Return the Hexagonal number Hk at k=k_min and above, which is also a pentagonal number
and a triangle number.

Problem statement:
Triangle, pentagonal, and hexagonal numbers are generated by the following formulae:
Triangle 	  	Tn=n(n+1)/2 	  	1, 3, 6, 10, 15, ...
Pentagonal 	  	Pn=n(3n−1)/2 	  	1, 5, 12, 22, 35, ...
Hexagonal 	  	Hn=n(2n−1) 	  	1, 6, 15, 28, 45, ...
It can be verified that T285 = P165 = H143 = 40755.
Find the next triangle number that is also pentagonal and hexagonal.
"""
function tri_penta_hexa_num(k_min=144)
    for k in Iterators.countfrom(k_min)
        j = (1 + √(1 + 24k*(2k-1)))/6
        if isinteger(j)
            #k is the index among hexagonal numbers (see Analysis below), so return Hk
            return k*(2k-1)
        end
    end
end

#=
Analysis:
i(i+1)/2 = j(3j-1)/2 = k(2k-1)
    Ti   =    Pj     =  Hk

    Hk = 2k(2k-1)/2 = T_2k-1_
So every odd term in Trinums is also a Hexanum.
Vice versa, every Hexanum is already a Trinum. 

3j² - j = 2Pj => 3j² - j - 2Pj = 0
j = (1 ± √(1 + 24Pj)/6 should be a positive integer
(1 ± √(1 + 24Hk)/6 should be a positive integer
(1 ± √(1 + 24k(2k-1))/6 should be a positive integer (for k ∈ N)
Since k is also a positive integer, only (1 + √(1 + 24k(2k-1))/6 can be a positive integer
(so exclude (1 - √(1 + 24k(2k-1))/6)
=#

isinteractive() || println(tri_penta_hexa_num())
